#define tn "ペイント支援ツール"
#define fn "ＭＳ ゴシック"
#packopt name tn
#packopt hide 1

randomize

#include "ClipboardModule.as"

// コマンド総数
#define command_num 7

#module
#defcfunc to_hsv int r, int g, int b
	max_value = 255
	max_h = 191
	max_s = 255
	max_v = 255
	
	DivH = (max_h + 1) / 6

	if r >= g & r >= b {
		max = r
		if g <= b {
			min = g
		} else {
			min = b
		}
		if min = max {
			h = 0
		} else {
			h = int(1.0 * DivH * (1.0 * (g - b) / (max - min)))
		}
	}
	else {
	if g >= r & g >= b {
		max = g
		if r <= b {
			min = r
		} else {
			min = b
		}
		if min = max {
			h = 0
		} else {
			h = int(1.0 * DivH * (1.0 * (b - r) / (max - min))) + 2 * DivH
		}
	}
	else {
		if b >= r & b >= g {
		max = b
		if r <= g {
			min = r
		} else {
			min = g
		}
		if min = max {
			h = 0
		} else {
			h = int(1.0 * DivH * (1.0 * (r - g) / (max - min))) + 4 * DivH
		}
	}
	else {
		h = 0
		max = min = r
	}
}
	}
	h = (h + (max_h + 1)) \ (max_h + 1)

	if max = 0 {
		s = 0
	} else {
		s = int(1.0 * max_s * (max - min) / max)
	}
	v = int(1.0 * max_v * max / max_value)

	return 1000000 * h + 1000 * s + v
#global

goto *@f
	// 輪郭を保ったまま拡大縮小用 変換先点の四隅座標
	#defcfunc zNewX1 int i_arg, int j_arg
		return int(zoom_w * i_arg)
	#defcfunc zNewX2 int i_arg, int j_arg
		return int(zoom_w * (i_arg + 1)) - 1
	#defcfunc zNewY1 int i_arg, int j_arg
		return int(zoom_h * j_arg)
	#defcfunc zNewY2 int i_arg, int j_arg
		return int(zoom_h * (j_arg + 1)) - 1
	
	// 輪郭を保ったまま拡大縮小用 指定した座標が輪郭点かどうか判定
	#defcfunc is_contour int i_arg, int j_arg
		// 画面外はすべて輪郭点であるとみなす
		if i_arg < 0 : return 1
		if j_arg < 0 : return 1
		if i_arg >= sx : return 1
		if j_arg >= sy : return 1
	
		// 画面内の点であれば、それが輪郭の色か判定した結果を返却
		pget i_arg, j_arg
		if ginfo_r = br & ginfo_g = bg & ginfo_b = bb {
			return 1
		}
		return 0
		
*@

frontFlg = 0
frontFlg_prev = frontFlg

// 初期値
thick_pt = 4
thick_color_r = 0
thick_color_g = 0
thick_color_b = 0
thick_back_color_r = 255
thick_back_color_g = 255
thick_back_color_b = 255

*main
onexit 0
screen 0, 320, 36 + 36 * command_num, , 50, 50
if frontFlg = 1 : gsel 0, 2 : else : gsel 0, 1
title tn
syscolor 15 : boxf
color
font fn, 16
objsize 320, 36
objmode 2
pos 10, 0 : chkbox "メインメニューを常に手前に表示する", frontFlg
pos 0, 36
button "画像を回転する", *bt : command_rotate = stat
button "指定色のみ抽出する", *bt : command_extract = stat
button "二つの画像を合成する", *bt : command_cover = stat
button "ランダム配色＋ぼかし", *bt : command_brushup = stat
button "画像の彩度を修正する", *bt : command_fix_s = stat
button "輪郭を太くする", *bt : command_thick_contour = stat
button "輪郭を保ったまま拡大縮小", *bt : command_zoominout = stat

*main_top
if frontFlg_prev ! frontFlg{
	frontFlg_prev = frontFlg
	if frontFlg = 1 : gsel 0, 2 : else : gsel 0, 1
}
wait 1
goto *main_top

*bt
frontFlg_prev = -1
gsel 0, 1
switch stat
case command_rotate : goto *rotate : swbreak
case command_extract : goto *extract : swbreak
case command_cover : goto *cover : swbreak
case command_brushup : goto *brushup : swbreak
case command_fix_s : goto *fix_s : swbreak
case command_thick_contour : goto *thick_contour : swbreak
case command_zoominout : goto *zoominout : swbreak
swend
stop

*getBmp
sx = 0 : sy = 0
ClipB_GetBmpSize sx, sy

if sx = 0 & sy = 0{
	dialog "クリップボードに画像をコピーしてください！", 1, tn
	return 1
}

buffer 1, sx, sy
pos 0, 0
ClipB_GetBmp 0, 0, sx, sy
redraw
gsel 0

return 0

*rotate
gosub *getBmp
if stat = 1 : goto *main_top
	
size = sqrt(sx * sx + sy * sy)
regular_size = 500
rate = 1.0 * regular_size / size
cx = regular_size / 2
cy = regular_size / 2
screen 0, regular_size, regular_size, , 50, 50
onexit *main
title "クリックで回転、EnterかSpaceで決定"

time = 0
angle = 0.0
click_flg = 0

*rotate_main
x = mousex
y = mousey
hsvcolor time, 255, 255
line -1, y, regular_size, y
line x, -1, x, regular_size

if click_flg = 1{
	angle = atan(y - cy, x - cx) - angle_prev
}
	
redraw 0
color : boxf
pos cx, cy
gmode 0, sx, sy, 256
grotate 1, 0, 0, angle, rate * sx, rate * sy
redraw

getkey key, 1
if key{
	if click_flg = 0{
		click_flg = 1
		angle_prev = atan(y - cy, x - cx) - angle
	}
}else{
	click_flg = 0
}

getkey key, 13
if key : goto *rotate_end
getkey key, 32
if key : goto *rotate_end

time++
await
goto *rotate_main

*rotate_end
buffer 2, size, size
color : boxf
pos size / 2, size / 2
gmode 0, sx, sy, 256
grotate 1, 0, 0, angle, sx, sy
ClipB_SetBmp 0, 0, size, size
goto *main

*extract
gosub *getBmp
if stat = 1 : goto *main_top

dialog "", 33
r = ginfo_r
g = ginfo_g
b = ginfo_b
screen 0, sx, sy, , 50, 50
onexit *main
for j, 0, sy
	for i, 0, sx
		gsel 1
		pget i, j
		if r = ginfo_r & g = ginfo_g & b = ginfo_b {
			gsel 0
			color r, g, b
			pset i, j
		}
	next
	gsel 0
	title "抽出中...(" + int(100.0 * j / sy + 0.5) + "％)"
	await
next
gsel 0
ClipB_SetBmp 0, 0, sx, sy
goto *main

*cover
gosub *getBmp
if stat = 1 : goto *main_top

sx0 = sx : sy0 = sy
buffer 2, sx0, sy0
pos 0, 0
gcopy 1, 0, 0, sx0, sy0

if sx0 > sy0{
	sx0new = 500
	sy0new = sx0new * sy0 / sx0
}else{
	sy0new = 500
	sx0new = sy0new * sx0 / sy0
}

screen 0, sx0new, sy0new, , 50, 50
title "画像1"
pos 0, 0
gzoom sx0new, sy0new, 2, 0, 0, sx0, sy0

dialog "これに合成する画像をクリップボードにコピーしたら\nOKを押してください。", , tn

gosub *getBmp
if stat = 1 : goto *main
if sx ! sx0 | sy ! sy0{
	dialog "二つの画像のサイズが異なります。\n同じサイズの画像を用意してください！", 1, tn
	goto *main
}

onexit *cover_exit

if sx > sy{
	sxnew = 500
	synew = sxnew * sy / sx
}else{
	synew = 500
	sxnew = synew * sx / sy
}

screen 3, sxnew, synew, , sx0new + 75, 50
title "画像2"
pos 0, 0
gzoom sxnew, synew, 1, 0, 0, sx, sy

screen 4, 240, 64, , 50, sy0new + 95
title "調整"
x = 1.0 : y = 1.0
pos 5, 5 : input x, 80, , 20 : cover_input_x = stat
pos 90, 5 : mes "："
pos 110, 5 : input y, 80, , 20 : cover_input_y = stat
pos 0, 32 : objsize 120, 32 : button "プレビュー", *cover_preview : cover_preview_button = stat
pos 120, 32 : objsize 120, 32 : button "これで確定", *cover_ok : cover_ok_button = stat

stop

*cover_exit
gsel 3, -1
gsel 4, -1
goto *main

*cover_preview
if x <= 0 | y <= 0 : dialog "比は正の数にしてください！", 1, tn : stop
gsel 0
pos 0, 0
gzoom sx0new, sy0new, 2, 0, 0, sx0, sy0
gsel 4
objenable cover_input_x, 0
objenable cover_input_y, 0
objenable cover_preview_button, 0
objenable cover_ok_button, 0
for j, 0, sy0new
	for i, 0, sx0new
		gsel 0 : pget i, j
		r1 = ginfo_r : g1 = ginfo_g : b1 = ginfo_b
		gsel 3 : pget i, j
		r2 = ginfo_r : g2 = ginfo_g : b2 = ginfo_b
		r = (1.0 * x * r1 + 1.0 * y * r2) / (x + y)
		g = (1.0 * x * g1 + 1.0 * y * g2) / (x + y)
		b = (1.0 * x * b1 + 1.0 * y * b2) / (x + y)
		gsel 0
		color r, g, b
		pset i, j
	next
	await
next
gsel 4
objenable cover_input_x, 1
objenable cover_input_y, 1
objenable cover_preview_button, 1
objenable cover_ok_button, 1
stop

*cover_ok
if x <= 0 | y <= 0 : dialog "比は正の数にしてください！", 1, tn : stop
gsel 4
cls
pos 5, 5 : mes "画像作成中..."
for j, 0, sy0
	for i, 0, sx0
		gsel 2 : pget i, j
		r1 = ginfo_r : g1 = ginfo_g : b1 = ginfo_b
		gsel 1 : pget i, j
		r2 = ginfo_r : g2 = ginfo_g : b2 = ginfo_b
		r = (1.0 * x * r1 + 1.0 * y * r2) / (x + y)
		g = (1.0 * x * g1 + 1.0 * y * g2) / (x + y)
		b = (1.0 * x * b1 + 1.0 * y * b2) / (x + y)
		gsel 2
		color r, g, b
		pset i, j
	next
	gsel 4
	title "合成中...(" + int(100.0 * j / sy + 0.5) + "％)"
	await
next
gsel 2
ClipB_SetBmp 0, 0, sx0, sy0
goto *cover_exit

*brushup
gosub *getBmp
if stat = 1 : goto *main_top

transFlg = 0
dialog "透明色（ぼかさない色）を適用する？\n適用する場合は(0, 0)のドットが\n透明色と見なされます。", 2, tn
if stat = 6 : transFlg = 1

if sx >= 500 | sy >= 500{
	dialog "これはドット絵向きの機能です。\n元の画像サイズが大きいため、時間が掛かる可能性があります。\nよろしいですか？\n画像サイズ: "+ sx +" × "+ sy, 3, tn
	if stat = 7 : goto *main_top
}

if transFlg = 1{
	gsel 1
	pget 0, 0
	tr = ginfo_r
	tg = ginfo_g
	tb = ginfo_b
}

screen 0, sx * 4, sy * 4, , 50, 50
onexit *main
gzoom sx * 4, sy * 4, 1, 0, 0, sx, sy
sx *= 4
sy *= 4

for j, 0, sy
	for i, 0, sx
		pget i, j
		if transFlg = 0 | ginfo_r ! tr | ginfo_g ! tg | ginfo_b ! tb{
			r = ginfo_r + rnd(61) - 30
			g = ginfo_g + rnd(61) - 30
			b = ginfo_b + rnd(61) - 30
			if r > 255 : r = 255
			if g > 255 : g = 255
			if b > 255 : b = 255
			if r < 0 : r = 0
			if g < 0 : g = 0
			if b < 0 : b = 0
			color r, g, b
			pset i, j
		}
	next
	title "ランダム配色中...(" + int(100.0 * j / sy + 0.5) + "％)"
	await
next

repeat (sx * sy) / 2
	x = (cnt * 2) \ sx : y = (cnt * 2) / sx
	pget x, y
	r = ginfo_r
	g = ginfo_g
	b = ginfo_b
	pget x, y - 1
	if transFlg = 0 | ginfo_r ! tr | ginfo_g ! tg | ginfo_b ! tb{
		color (r + ginfo_r) / 2, (g + ginfo_g) / 2, (b + ginfo_b) / 2
		pset x, y - 1
	}
	pget x, y + 1
	if transFlg = 0 | ginfo_r ! tr | ginfo_g ! tg | ginfo_b ! tb{
		color (r + ginfo_r) / 2, (g + ginfo_g) / 2, (b + ginfo_b) / 2
		pset x, y + 1
	}
	pget x - 1, y
	if transFlg = 0 | ginfo_r ! tr | ginfo_g ! tg | ginfo_b ! tb{
		color (r + ginfo_r) / 2, (g + ginfo_g) / 2, (b + ginfo_b) / 2
		pset x - 1, y
	}
	pget x + 1, y
	if transFlg = 0 | ginfo_r ! tr | ginfo_g ! tg | ginfo_b ! tb{
		color (r + ginfo_r) / 2, (g + ginfo_g) / 2, (b + ginfo_b) / 2
		pset x + 1, y
	}
	if cnt \ 100 = 0{
		title "ぼかし適用中...(" + int(100.0 * cnt / ((sx * sy) / 2) + 0.5) + "％)"
		await
	}
loop

gsel 1
gzoom sx / 4, sy / 4, 0, 0, 0, sx, sy
ClipB_SetBmp 0, 0, sx / 4, sy / 4
goto *main

*fix_s
gosub *getBmp
if stat = 1 : goto *main_top

screen 0, sx, sy, , 50, 50
onexit *main
pos 0, 0
gcopy 1, 0, 0, sx, sy
for j, 0, sy
	for i, 0, sx
		pget i, j
		r = ginfo_r
		g = ginfo_g
		b = ginfo_b
	
		hsv = to_hsv(r, g, b)
		hsv_h = hsv / 1000000
		hsv_s = hsv / 1000 \ 1000
		hsv_v = hsv \ 1000
		
		hsv_s = hsv_s * 2 / 3
		hsv_v = hsv_v * 210 / 255 + 45
		
		hsvcolor hsv_h, hsv_s, hsv_v
		pset i, j
	next
	await
	title "修正中...(" + int(100.0 * j / sy + 0.5) + "％)"
next

gsel 0
ClipB_SetBmp 0, 0, sx, sy
goto *main

*thick_contour
gosub *getBmp
if stat = 1 : goto *main_top

screen 0, 260, 160
onexit *main
cls 1
pos 5, 5  : mes "輪郭の太さ：" : pos 110, 3 : input thick_pt, 32, 24, 5
pos 5, 45 : mes "輪郭の色　：" : boxf 110, 38, 110 + 32, 38 + 32 : color thick_color_r, thick_color_g, thick_color_b : boxf 110 + 1, 38 + 1, 110 + 31, 38 + 31
pos 5, 85 : mes "背景の色　：" : boxf 110, 78, 110 + 32, 78 + 32 : color thick_back_color_r, thick_back_color_g, thick_back_color_b : boxf 110 + 1, 78 + 1, 110 + 31, 78 + 31
pos 160, 43 : button "変更", *thick_contour_change_color
pos 160, 83 : button "変更", *thick_contour_change_back_color
pos 180, 130 : button "OK", *thick_contour_ok
stop

*thick_contour_change_color
dialog "", 33
thick_color_r = ginfo_r
thick_color_g = ginfo_g
thick_color_b = ginfo_b
color thick_color_r, thick_color_g, thick_color_b : boxf 110 + 1, 38 + 1, 110 + 31, 38 + 31
stop

*thick_contour_change_back_color
dialog "", 33
thick_back_color_r = ginfo_r
thick_back_color_g = ginfo_g
thick_back_color_b = ginfo_b
color thick_back_color_r, thick_back_color_g, thick_back_color_b : boxf 110 + 1, 78 + 1, 110 + 31, 78 + 31
stop

*thick_contour_ok
screen 0, sx, sy, , 50, 50
onexit *main
color thick_back_color_r, thick_back_color_g, thick_back_color_b : boxf
for j, 0, sy
	for i, 0, sx
		gsel 1
		pget i, j
		if ginfo_r = thick_color_r & ginfo_g = thick_color_g & ginfo_b = thick_color_b {
			gsel 0
			color thick_color_r, thick_color_g, thick_color_b
			circle i - thick_pt, j - thick_pt, i + thick_pt, j + thick_pt
		}
	next
	await
	title "修正中...(" + int(100.0 * j / sy + 0.5) + "％)"
next

gsel 0
gmode 4, sx, sy, 256
color thick_back_color_r, thick_back_color_g, thick_back_color_b
pos 0, 0
gcopy 1, 0, 0, sx, sy
gmode 0

gsel 0
ClipB_SetBmp 0, 0, sx, sy
goto *main

*zoominout
gosub *getBmp
if stat = 1 : goto *main_top

// サイズ変更モード
gsel 0, -1
screen 2, ginfo_dispx, ginfo_dispy, 8, 50, 50
onexit *zoominout_cancel
cls 4
width sx, sy

*zoominout_change_size_mode

// 拡大率をウィンドウサイズで決定
zoom_w = (1.0 * ginfo_winx) / sx
zoom_h = (1.0 * ginfo_winy) / sy
	
title "ウィンドウサイズ変更、EnterかSpaceで決定(" + strf("%4.2f", zoom_w * 100.0) + "%," + strf("%4.2f", zoom_h * 100.0) + "%)"

redraw 0
color : boxf
pos 0, 0
gzoom ginfo_winx, ginfo_winy, 1, 0, 0, sx, sy, 0
redraw

getkey key, 13
if key : goto *zoominout_change_main_process
getkey key, 32
if key : goto *zoominout_change_main_process

wait 1
goto *zoominout_change_size_mode

*zoominout_change_main_process
gsel 2, -1

// 輪郭色
br = 0
bg = 0
bb = 0

// 拡大縮小後のウィンドウサイズ
sx2 = int(zoom_w * sx + 0.5)
sy2 = int(zoom_h * sy + 0.5)

screen 0, sx2, sy2, , 50, 50
onexit *main

// 各点を調べて、輪郭点の場合は周りとの繋がりを考慮した描画をしていく
for j, 0, sy
	for i, 0, sx
		gsel 1
		pget i, j
		
		r = ginfo_r
		g = ginfo_g
		b = ginfo_b
		
		if r = br & g = bg & b = bb {
			// 黒点の場合は繋がりも取得する

			// 変換後の、中心の(x, y)座標
			cx = (zNewX1(i, j) + zNewX2(i, j)) / 2
			cy = (zNewY1(i, j) + zNewY2(i, j)) / 2
	
			// 8方向に確認していく
			for k, 0, 8
	
				// 取得対象をcenterとし、そこから左回り方向をleft、右回り方向をrightとする
				// まずはその座標をそれぞれ取得しておく
				switch k
				case 0	// 左上
					x_left   = i - 1 : y_left   = j
					x_center = i - 1 : y_center = j - 1
					x_right  = i     : y_right  = j - 1
					swbreak
				case 1	// 上
					x_left   = i - 1 : y_left   = j - 1
					x_center = i     : y_center = j - 1
					x_right  = i + 1 : y_right  = j - 1
					swbreak
				case 2	// 右上
					x_left   = i     : y_left   = j - 1
					x_center = i + 1 : y_center = j - 1
					x_right  = i + 1 : y_right  = j
					swbreak
				case 3	// 右
					x_left   = i + 1 : y_left   = j - 1
					x_center = i + 1 : y_center = j
					x_right  = i + 1 : y_right  = j + 1
					swbreak
				case 4	// 右下
					x_left   = i + 1 : y_left   = j
					x_center = i + 1 : y_center = j + 1
					x_right  = i     : y_right  = j + 1
					swbreak
				case 5	// 下
					x_left   = i + 1 : y_left   = j + 1
					x_center = i     : y_center = j + 1
					x_right  = i - 1 : y_right  = j + 1
					swbreak
				case 6	// 左下
					x_left   = i     : y_left   = j + 1
					x_center = i - 1 : y_center = j + 1
					x_right  = i - 1 : y_right  = j
					swbreak
				case 7	// 左
					x_left   = i - 1 : y_left   = j + 1
					x_center = i - 1 : y_center = j
					x_right  = i - 1 : y_right  = j - 1
					swbreak
				swend
	
				// 変換後の各座標を求めておく
				x_left2 = (zNewX1(x_left, y_left) + zNewX2(x_left, y_left)) / 2
				y_left2 = (zNewY1(x_left, y_left) + zNewY2(x_left, y_left)) / 2
				x_center2 = (zNewX1(x_center, y_center) + zNewX2(x_center, y_center)) / 2
				y_center2 = (zNewY1(x_center, y_center) + zNewY2(x_center, y_center)) / 2
				x_right2 = (zNewX1(x_right, y_right) + zNewX2(x_right, y_right)) / 2
				y_right2 = (zNewY1(x_right, y_right) + zNewY2(x_right, y_right)) / 2

				// それぞれの座標に輪郭点があれば1、なければ0とする
				gsel 1
				if is_contour(x_left, y_left)     : p_left = 1   : else : p_left = 0
				if is_contour(x_center, y_center) : p_center = 1 : else : p_center = 0
				if is_contour(x_right, y_right)   : p_right = 1  : else : p_right = 0

				// 輪郭点のパターンによって配色が変わる
				gsel 0
				color br, bg, bb
				if p_left = 0 & p_center = 0 & p_right = 0 {
					// 何も起こらない
				}
				if p_left = 1 & p_center = 0 & p_right = 0 {
					// 線で繋ぐだけ
					line x_left2, y_left2, cx, cy
				}
				if p_left = 0 & p_center = 1 & p_right = 0 {
					// 線で繋ぐだけ
					line x_center2, y_center2, cx, cy
				}
				if p_left = 0 & p_center = 0 & p_right = 1 {
					// 線で繋ぐだけ
					line x_right2, y_right2, cx, cy
				}
				if p_left = 1 & p_center = 0 & p_right = 1 {
					// 二本の線でそれぞれ繋ぐ
					line x_left2, y_left2, cx, cy
					line x_right2, y_right2, cx, cy
				}
				if p_left = 1 & p_center = 1 & p_right = 0 {
					// 三角形で塗りつぶし
					xary = cx, cx, x_left2, x_center2
					yary = cy, cy, y_left2, y_center2
					gsquare -1, xary, yary
				}
				if p_left = 0 & p_center = 1 & p_right = 1 {
					// 三角形で塗りつぶし
					xary = cx, cx, x_center2, x_right2
					yary = cy, cy, y_center2, y_right2
					gsquare -1, xary, yary
				}
				if p_left = 1 & p_center = 1 & p_right = 1 {
					// 四角形で塗りつぶし
					xary = cx, x_left2, x_center2, x_right2
					yary = cy, y_left2, y_center2, y_right2
					gsquare -1, xary, yary
				}

			next
	
			// 中心点は描画しておく
			gsel 0
			color br, bg, bb
			pset cx, cy
		
		} else {
			// 黒点以外はそのまま転写する
			// （本来は境界線をもとに色分け考えるべきなのだろうが、ここでは考慮対象外とする）
	
			x1 = zNewX1(i, j)
			x2 = zNewX2(i, j)
			y1 = zNewY1(i, j)
			y2 = zNewY2(i, j)
	
			gsel 0
			color r, g, b
			boxf x1, y1, x2, y2
		}

	next
	gsel 0
	title "処理中...(" + int(100.0 * j / sy + 0.5) + "％)"
	await
next

gsel 0
ClipB_SetBmp 0, 0, sx2, sy2
goto *main

*zoominout_cancel
	gsel 2, -1
	goto *main
	